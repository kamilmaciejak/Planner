apply plugin: 'com.android.application'
apply plugin: 'org.greenrobot.greendao'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "com.example.planner"
        minSdkVersion 19
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
    }
    signingConfigs {
        release {
            storeFile file("keystore.jks")
            storePassword "Password1"
            keyAlias "Planner"
            keyPassword "Password1"
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    dataBinding {
        enabled = true
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    flavorDimensions "version"
    productFlavors {
        dev {
            applicationIdSuffix ".dev"
            versionNameSuffix "-dev"
        }
        prod {
        }
    }
    applicationVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "planner-${variant.versionName}.apk"
        }
    }
    variantFilter { variant ->
        def buildType = variant.buildType.name
        def flavorsNames = variant.flavors*.name
        if (buildType == "release" && flavorsNames.contains("dev")) {
            setIgnore(true)
        }
    }
}

greendao {
    schemaVersion 7
}

ext {
    supportLibraryVersion = "28.0.0"
    koinVersion = "1.0.2"
    leakCanaryVersion = "1.6.2"
    rxAndroidVersion = "2.1.0"
    rxJavaVersion = "2.2.5"
    stethoVersion = "1.5.0"
    timberVersion = "4.7.1"
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // support library
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"

    // constraint layout
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'

    // greenDao
    implementation "org.greenrobot:greendao:$greenDaoVersion"

    // koin
    implementation "org.koin:koin-android:$koinVersion"
    implementation "org.koin:koin-android-scope:$koinVersion"
    implementation "org.koin:koin-android-viewmodel:$koinVersion"

    // leakCanary
    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    // Optional, if you use support library fragments:
    // debugImplementation "com.squareup.leakcanary:leakcanary-support-fragment:$leakcanaryVersion"

    // rxJava
    implementation "io.reactivex.rxjava2:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex.rxjava2:rxjava:$rxJavaVersion"

    // stetho
    implementation "com.facebook.stetho:stetho:$stethoVersion"

    // timber
    implementation "com.jakewharton.timber:timber:$timberVersion"

    // view model
    implementation "android.arch.lifecycle:extensions:1.1.1"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
